
#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если  ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);   
		ДобавитьГруппуРаботыСоСкидками(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
		
    ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);   
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);   
	
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);  
		
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	//Отображение поля "Контакное лицо"
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;	
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.КонтактноеЛицо";   
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо"; 
	
КонецПроцедуры

Процедура ДобавитьГруппуРаботыСоСкидками(Форма)
	
	//Добавление группы с полем скидки и кнопкой "Пересчитать таблицу"
	ЭлементГруппаСкидки = Форма.Элементы.Вставить("ГруппаСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ЭлементГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	//Отображение поля "Согласованная скидка"
	ЭлементСогласованнаяСкидка = Форма.Элементы.Вставить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;  
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.СогласованнаяСкидка"; 
	ЭлементСогласованнаяСкидка.Заголовок = "Согласованная скидка";
	ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "УточнитьСогласованнуюСкидку");
	
	//Отображение поля "Согласованная скидка"
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "ПересчитатьТаблицуИИтогДокумента";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	

КонецПроцедуры	


Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	//Отображение поля "Контакное лицо"
	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;	
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.КонтактноеЛицо";   
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо"; 
	
КонецПроцедуры

#КонецОбласти

